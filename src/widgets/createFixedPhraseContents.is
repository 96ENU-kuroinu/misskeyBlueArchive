/*==================== メタデータ ====================*/
### {
  name: "作品投稿定型文生成ウィジェット"
  version: 1
}

/*==================== 変数 ====================*/
// 定型文生成のために必要な変数(UI変更しても以下の値は残す)
var resultText      = ""
var showPreview     = false
var openBracket     = ""

// 本UIでの挙動に必要な変数
var pixivText       = ""
var niconicoText    = ""
var xfolioText      = ""

/*==================== 定数 ====================*/
// 定型文生成のために必要な定数(UI変更しても以下の値は残す)
let OPEN_BRACKET                    = "("       // URL用開きかっこ
let OPEN_BRACKET_WITH_PREVIEW       = "["       // プレビューありURL用開きかっこ
let OPEN_BRACKET_WITHOUT_PREVIEW    = "?["      // プレビューなしURL用開きかっこ
let CLOSE_BRACKET                   = ")"       // 閉じかっこ(小かっこ)
let CLOSE_BRACKET_SQUARE            = "]"       // 閉じかっこ(大かっこ)

// 本UIでの挙動に必要な定数
let PIXIV_HEADER            = ":pixiv:に投稿しました"                   // pixiv用定型文
let NICONICO_HEADER         = ":nicotta::nicotta:静画に投稿しました"    // ニコニコ静画用定型文
let XFOLIO_HEADER           = "Xfolioに投稿しました"                    // Xfolio用定型文

/*
    投稿用の定型文を生成する
*/
@createNoteText(){
    resultText = ""
    resultText = createUrlText(resultText, PIXIV_HEADER, pixivText)
    resultText = createUrlText(resultText, NICONICO_HEADER, niconicoText)
    resultText = createUrlText(resultText, XFOLIO_HEADER, xfolioText)

    if (resultText == "") {
        resultText = "何も入力されていません！"
    } else {
        resultText = ["```", Str:lf, resultText, Str:lf, "```"].join()
    }

    Ui:get("result").update({text: resultText})
}

/*
    リンクテキストを生成する
    resultText  生成前の時点での結果(ここに付け足す)
    headerText  リンク化する定型文
    urlText     リンク先のURL
*/
@createUrlText(resultText, headerText, urlText){
    if (urlText == "") {
        return resultText
    }

    if (showPreview) {
        openBracket = OPEN_BRACKET_WITH_PREVIEW
    } else {
        openBracket = OPEN_BRACKET_WITHOUT_PREVIEW
    }

    if (resultText != "") {
        resultText = [resultText, Str:lf].join()
    }

    return [resultText, openBracket, headerText, CLOSE_BRACKET_SQUARE, OPEN_BRACKET, urlText, CLOSE_BRACKET].join()
}

Ui:render([
    Ui:C:folder({
        children: [
            Ui:C:switch({
            onChange:   @(enabled){ showPreview = enabled }
            default:    false
            label:      "リンクプレビューを表示する"
            })

            Ui:C:textInput({
                default:     ""
                onInput:    @(text){ pixivText = text }
                label:      "pixiv URL"
            } "pixivTextInput")

            Ui:C:textInput({
                default:    ""
                onInput:    @(text){ niconicoText = text }
                label:      "ニコニコURL"
            } "niconicoTextInput")

            Ui:C:textInput({
                default:    ""
                onInput:    @(text){ xfolioText = text }
                label:      "Xfolio URL"
            } "XfolioTextInput")

            Ui:C:button({
                text:       "投稿文を生成"
                onClick:    @(){createNoteText()}
                primary:    false // 色を付けて強調
                rounded:    false // 角を丸く
                disabled:   false // 無効化
            })

            Ui:C:mfm({
            } "result" )
        ]
        title: "作品投稿定型文生成"
        opened: false // 開いているか
    })
])